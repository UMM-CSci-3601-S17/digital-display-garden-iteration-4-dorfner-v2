// Server side build.gradle
// Builds the server-side of the project

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'umm3601.Server'

dependencies {
    compile 'com.sparkjava:spark-core:2.5.4'
    compile 'org.slf4j:slf4j-simple:1.7.6'
    compile 'com.google.code.gson:gson:2.8.0'
    compile files('./src/libs/3601-lab3-todos.jar')
    compile 'org.mongodb:mongodb-driver:3.4.2'
    compile 'org.apache.poi:poi-ooxml:3.15'
    compile 'joda-time:joda-time:2.9.9'

    //compile 'sun.text.normalizer.UTF16: '
    compile 'com.google.zxing:core:2.2'
    compile 'com.google.zxing:javase:2.2'

    compile 'com.github.scribejava:scribejava-apis:4.1.0'
    compile 'io.jsonwebtoken:jjwt:0.7.0'
    compile 'com.nimbusds:nimbus-jose-jwt:4.23'

    testCompile 'junit:junit:4.12'
}

distributions {
    main {
        contents {
            // Put the client artifacts (HTML, CSS, and JS generated
            // by Angular) in the `lib/client/public` directory. This
            // plus putting `lib/client` in the classpath (below in this
            // file) and setting `public` as the place where static files
            // will be served from by Spark (in `Server.java`) allows
            // the generated static files to be successfully served from
            // Spark when run from the gradle generated startup script
            // in the gradle generated tarball.
            into('lib/client/public') {
                from { project(':client').build.outputs.files }
                fileMode = 0755
            }
        }
    }
}

task runWithTests {
    group = "application"
    description = "Runs the server as a JVM application after testing both it and the client"
}

runWithTests.dependsOn test

test.dependsOn(":client:runClientTests")

// Adds the `lib/client` directory to the classpath that is built
// in the startup script that gradle builds for us. It appears that
// whatever we put here has `lib/` prepended before it, which is kinda
// annoying for the static web artifacts, but this does work.
startScripts {
    classpath += files('client')
    unixStartScriptGenerator.template = project.resources.text.fromFile("script-template")
}
